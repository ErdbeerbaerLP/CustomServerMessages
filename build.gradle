buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven"}
        maven {
            name = 'sponge'
            url = 'https://repo.spongepowered.org/maven'
        }
    }
    dependencies {
        classpath 'org.spongepowered:mixingradle:0.6-SNAPSHOT'
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath "com.github.jengelman.gradle.plugins:shadow:4.0.4"
    }
}
plugins {
    id "com.matthewprenger.cursegradle" version "1.1.0"
}
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'org.spongepowered.mixin'
//Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.


version = "3.0.0"
group = "de.erdbeerbaerlp.customServerMessages" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "customServerMessages"

sourceCompatibility = targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}
repositories {
    jcenter()
    maven {
        name = 'sponge'
        url = 'https://repo.spongepowered.org/maven'
    }
    maven { url 'https://jitpack.io' }
}
configurations {
    embed
    compile.extendsFrom(embed)
}
minecraft {
    version = "1.12.2-14.23.5.2847"
    runDir = "run"
    
    // the mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   snapshot are built nightly.
    // stable_#            stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not always work.
    // simply re-run your setup task after changing the mappings to update your workspace.
    mappings = "snapshot_20171003"
    makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.
}
reobf {
    shadowJar { mappingType = 'SEARGE' }
}
shadowJar {
    classifier = '1.12'
    configurations = [project.configurations.embed]
}
dependencies {
    embed("org.spongepowered:mixin:0.8-SNAPSHOT") {
        transitive = false
    }
}
tasks.build.dependsOn reobfShadowJar
mixin {
    add sourceSets.main, 'mixins.customservermsgs.refmap.json'
}
processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else except the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}
jar {
    classifier '1.12'
    manifest {
        attributes([
                'FMLAT': 'customservermsgs_at.cfg',
                'Maven-Artifact': "${project.group}:${project.archivesBaseName}:${project.version}",
                'Timestamp': System.currentTimeMillis(),
                'TweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
                'MixinConfigs': 'mixins.customservermsgs.init.json',
                "ForceLoadAsMod": "true",
                'FMLCorePluginContainsFMLMod': 'true'
        ])
    }
}