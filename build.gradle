buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        mavenCentral()
    }

    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1+', changing: true
        classpath 'org.spongepowered:mixingradle:0.7.32'
    }
}
plugins {
    id "com.matthewprenger.cursegradle" version "1.4.0"
    id 'maven-publish'
    id 'java'
    id 'com.github.johnrengelman.shadow' version "7.0.0"
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'
//Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.


version = "5.0.0"
group = "de.erdbeerbaerlp.customServerMessages" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "customServerMessages"

java.toolchain.languageVersion = JavaLanguageVersion.of(17)
repositories {
    maven {
        name = 'sponge'
        url = 'https://repo.spongepowered.org/maven'
    }
    maven { url 'https://jitpack.io' }
    maven { url "https://repo.erdbeerbaerlp.de/repository/maven-public/" }
}
configurations {
    embed
    compileOnly.extendsFrom(embed)
}
minecraft {
    mappings channel: 'official', version: '1.19.2'
    //accessTransformer = file('build/resources/main/META-INF/accesstransformer.cfg')
}
reobf {
    shadowJar {
        dependsOn tasks.createMcpToSrg
        mappings = tasks.createMcpToSrg.outputs.files.singleFile
    }
}
shadowJar {
    classifier = '1.19.2'
    configurations = [project.configurations.embed]
    relocate "com.google.gson", "csmshadow.com.google.gson"
    relocate "com.moandjiezana", "csmshadow.com.moandjiezana"
}
dependencies {
    minecraft 'net.minecraftforge:forge:1.19.2-43.1.47'

    // Apply Mixin AP
    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
    embed("de.erdbeerbaerlp:toml4j:2073e86")
}
tasks.build.dependsOn reobfShadowJar
mixin {
    add sourceSets.main, 'mixins.customservermsgs.refmap.json'
}
jar {
    classifier = "1.19.2"
    manifest {
        attributes([
                'Maven-Artifact'          : "${project.group}:${project.archivesBaseName}:${project.version}",
                'Timestamp'               : System.currentTimeMillis(),
                "Specification-Title"     : "customservermsgs",
                "Specification-Vendor"    : "customservermsgs",
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : "${version}",
                "Implementation-Vendor"   : "customservermsgs",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                'MixinConnector': 'de.erdbeerbaerlp.customServerMessages.MixinConnector'
        ])
    }
}
curseforge {
    // $GRADLE_USER_HOME/gradle.properties
    if (project.hasProperty('curseforge.apikey')) {
        apiKey = getProperty("curseforge.apikey")
        project {
            id = '301614'
            changelog = project.changelog
            releaseType = 'beta'
            addGameVersion '1.19.2'
            mainArtifact(jar) {
                displayName = "CustomServerMessages-$version (MC 1.19.2)"
            }
        }
    }
}
tasks.curseforge.dependsOn(build)