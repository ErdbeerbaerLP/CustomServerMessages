buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven { url = "http://files.minecraftforge.net/maven" }
        maven {
            name = 'sponge'
            url = 'https://repo.spongepowered.org/maven'
        }
        repositories {
            maven {
                url "https://plugins.gradle.org/m2/"

            }
        }
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
        classpath "com.github.jengelman.gradle.plugins:shadow:4.0.4"
    }
}
plugins {
    id "com.matthewprenger.cursegradle" version "1.1.0"
}
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'
//Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.


version = "4.0.0"
group = "de.erdbeerbaerlp.customServerMessages" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "customServerMessages"

sourceCompatibility = targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}
repositories {
    jcenter()
    maven {
        name = 'sponge'
        url = 'https://repo.spongepowered.org/maven'
    }
    maven { url 'https://jitpack.io' }
}
configurations {
    embed
    compile.extendsFrom(embed)
}
minecraft {
    mappings channel: 'snapshot', version: '20200612-1.15.1'
    //accessTransformer = file('build/resources/main/META-INF/accesstransformer.cfg')

    // Default run configurations.
    // These can be tweaked, removed, or duplicated as needed.
    runs {
        client {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

            mods {
                dcintegration {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

            mods {
                dcintegration {
                    source sourceSets.main
                }
            }
        }
    }
}
reobf {
    shadowJar {
        dependsOn tasks.createMcpToSrg
        mappings = tasks.createMcpToSrg.outputs.files.singleFile
    }
}
shadowJar {
    classifier = '1.15'
    configurations = [project.configurations.embed]
    relocate "com.google", "csmshadow.com.google"
    relocate "com.moandjiezana", "csmshadow.com.moandjiezana"
}
dependencies {
    minecraft 'net.minecraftforge:forge:1.15.2-31.2.9'
    compile("org.spongepowered:mixin:0.8-SNAPSHOT") {
        transitive = false
    }
    embed "com.github.ErdbeerbaerLP:toml4j:wip-SNAPSHOT"
}
tasks.build.dependsOn reobfShadowJar
mixin {
    add sourceSets.main, 'mixins.customservermsgs.refmap.json'
}
jar {
    classifier = "1.15"
    manifest {
        attributes([
                'Maven-Artifact'          : "${project.group}:${project.archivesBaseName}:${project.version}",
                'Timestamp'               : System.currentTimeMillis(),
                "Specification-Title"     : "customservermsgs",
                "Specification-Vendor"    : "customservermsgs",
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : "${version}",
                "Implementation-Vendor"   : "customservermsgs",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                'MixinConnector': 'de.erdbeerbaerlp.customServerMessages.CSMMixinConnector'
        ])
    }
}
curseforge {
    // $GRADLE_USER_HOME/gradle.properties
    if (project.hasProperty('curseforge.apikey')) {
        apiKey = getProperty("curseforge.apikey")
        project {
            id = '301614'
            changelog = project.changelog
            releaseType = 'beta'
            addGameVersion '1.15.2'
            mainArtifact(jar) {
                displayName = "CustomServerMessages-$version (MC 1.15)"
            }
            relations {
                requiredDependency 'mixinbootstrap'
            }
            options {
                forgeGradleIntegration = false // defaults to true
            }
        }
    }
}
tasks.curseforge.dependsOn(build)